      SUBROUTINE NODLEX
     *   (NODE,ME,NE,NP,N1,NEX,SN,
     *    IPART,LDOM,NBPDOM,NDOM,IPSLF,IPSND,MBPDOM,
     *    VALELM,VALNOD,CM,IUT0,IERR,BUFSND,BUFRCV,MAXBUF)
C
      IMPLICIT NONE
C
      INTEGER*4 ME,NE,NP,N1
      INTEGER*4 NODE(N1,NE),NEX(8)
      REAL*4 SN (N1,ME)
      INTEGER*4 IPART,NDOM,MBPDOM
      INTEGER*4 LDOM(NDOM),NBPDOM(NDOM)
      INTEGER*4 IPSLF(MBPDOM,NDOM),IPSND(MBPDOM,NDOM)
      REAL*4 VALELM(NE),VALNOD(NP),CM(NP)
      INTEGER*4 IUT0,IERR,MAXBUF
      REAL*4 BUFSND(MAXBUF),BUFRCV(MAXBUF)
C
      CHARACTER*60 ERMSGC
     & /' ## SUBROUTINE NODLEX: FATAL      ERROR REPORT   ; RETURNED' /
C
      INTEGER*4 NEHEX, NHEX
C
      INTEGER*4 I,IE,IP,IDUM
C
C     * START *
C
      NEHEX=NEX(4)
      NHEX =NEX(8)
C
C
C     *** ZERO ***
C
C
      DO 300 IP=1,NP
         VALNOD(IP)=0.0E0
 300  CONTINUE
C
C     
C     *** VALNOD = N * VALELM ***
C
C     * HEX *
      DO 107 I=1,NHEX
         DO 106 IE=1,NEHEX
            IP = NODE(I,IE)
            VALNOD(IP) = VALNOD(IP) + SN(I,IE)*VALELM(IE)
 106     CONTINUE
 107  CONTINUE
C
C
C     *** DATA COMMUNICATION ***
C
C
      IDUM=1
      CALL DDCOMX(IPART,IDUM,LDOM,NBPDOM,NDOM,IPSLF,IPSND,MBPDOM,
     &            VALNOD,VALNOD,VALNOD,NP,IUT0,IERR,
     &            BUFSND,BUFRCV,MAXBUF) 
      IF(IERR.NE.0) THEN
         WRITE(IUT0,*)
         WRITE(IUT0,*) ERMSGC
         RETURN
      END IF   
C
C
C     *** INVERSE MASS MATRIX ***
C
C
      DO 200 IP=1,NP
         VALNOD(IP)=VALNOD(IP)*CM(IP)
 200  CONTINUE
C
      RETURN
      END
